				#include <p18f452.inc>
				CONFIG	WDT = OFF
				LIST	P = 18F452	
				RADIX 	HEX

#define 		RS_PIN			LATC,LATC0
#define 		RW_PIN			LATC,LATC1
#define			EN_PIN			LATC,LATC2
#define 		LCD_DATA_BUS	LATD

STATE			EQU		0x000
COUNTER			EQU		0x001

				ORG		0x01000
STRING_1		DB		"NO BUTTON", '\0'
				ORG		0x00F00
STRING_2		DB		"BUTTON   ", '\0'

				ORG		0x00000
				GOTO	MAIN
				ORG		0x00008
				GOTO	INTERRUPT
MAIN
				CLRF	TRISD,0
				CLRF	COUNTER,0
				MOVLW	0xF8
				MOVWF	TRISC,0
				CALL	LCD_INIT
				BSF		INTCON,PEIE	
				BSF		INTCON,GIE
				BSF		INTCON,INT0IE			
				BCF		INTCON,INT0IF
				BCF		INTCON2,INTEDG0
				BSF		PIE1,TMR1IE
				BCF		PIR1,TMR1IF
				MOVLW	0xB0
				MOVWF	T1CON
				MOVLW	0x0B
				MOVWF	TMR1H
				MOVLW	0xDC
				MOVWF	TMR1L
				BSF		T1CON,TMR1ON
				GOTO	$
INTERRUPT
				BTFSC	INTCON,INT0IF
				GOTO	BUTTON_PRESSED
				BTFSC	PIR1,TMR1IF
				GOTO	TIMER1_OVERFLOWED
				GOTO 	RETURN_HOME
TIMER1_OVERFLOWED
				BCF		PIR1,TMR1IF
				MOVLW	0x0B
				MOVWF	TMR1H
				MOVLW	0xDC
				MOVWF	TMR1L
				MOVF	COUNTER,W
				XORLW	0x09
				BZ		DISPLAY
				INCF	COUNTER,F,0
;---------------------------------------
RETURN_HOME
				MOVLW	0x80
				CALL	COMMAND_SEND
				CALL	BUSY_FLAG
				RETFIE
;----------------------------------------
DISPLAY
				CLRF	COUNTER,0
				MOVLW	0x00
				MOVWF	TBLPTRL
				MOVLW	0x10
				MOVWF	TBLPTRH
				GOTO	DISPLAY_STRING			
BUTTON_PRESSED
				BCF		INTCON,INT0IF		
				CLRF	COUNTER,0
				MOVLW	0x0B
				MOVWF	TMR1H
				MOVLW	0xDC
				MOVWF	TMR1L
				MOVLW	0x00
				MOVWF	TBLPTRL
				MOVLW	0x0F
				MOVWF	TBLPTRH	
				GOTO	DISPLAY_STRING
DISPLAY_STRING
				TBLRD*
				MOVF	TABLAT,W
				XORLW	'\0'
				BTFSC	STATUS,Z
				GOTO 	RETURN_HOME
				MOVF	TABLAT,W
				CALL	DATA_SEND
				INCF	TBLPTRL,F,0
				GOTO	DISPLAY_STRING
LCD_INIT							; LCD INITIALIZATION(FUNCTION SET) : 2 LINES, 5*7 PIXEL MODE, 8 BIT MODE INTERFACE
				MOVLW	0x38
				CALL	COMMAND_SEND
				CALL	DELAY_250_MS
				MOVLW	0x01		; CLEAR HOME
				CALL	COMMAND_SEND
				CALL	DELAY_250_MS
				CALL	DELAY_250_MS
				MOVLW	0x0C		; LCD ON, CURSOR OFF
				CALL	COMMAND_SEND
				CALL	DELAY_250_MS
				RETURN
DELAY_250_MS
				MOVLW	0x01
				MOVWF	T0CON
				MOVLW	0x0B
				MOVWF	TMR0H
				MOVLW	0xDC
				MOVWF	TMR0L
				CALL	TIMER_0
				RETURN
TIMER_0
				BCF		INTCON,TMR0IF
				BSF		T0CON,TMR0ON		
WAIT
				BTFSS	INTCON,TMR0IF
				GOTO	WAIT
				BCF		INTCON,TMR0IF
				BCF		T0CON,TMR0ON
				RETURN
DELAY_450_US
				MOVLW	0x40
				MOVWF	T0CON
				MOVLW	D'31'
				MOVWF	TMR0L
				CALL	TIMER_0
				RETURN
DATA_SEND								; SUBROUTINE TO HANDL DATA SENDED TO LCD
				MOVWF	LCD_DATA_BUS
				BSF		RS_PIN
				BCF		RW_PIN
				BSF		EN_PIN
				CALL	DELAY_450_US
				BCF		EN_PIN
				CALL	BUSY_FLAG
				RETURN
COMMAND_SEND							; SUBROUTINE TO HANDL COMMAND SENDED TO LCD
				MOVWF	LCD_DATA_BUS
				BCF		RS_PIN
				BCF		RW_PIN
				BSF		EN_PIN
				CALL	DELAY_450_US			
				BCF		EN_PIN
				RETURN	
BUSY_FLAG							; BUSY FLAG TO CHECK IF LCD CONTROLLER IS READY OR BUSY 
				BSF		TRISD,TRISD7
				BCF		RS_PIN
				BSF		RW_PIN
CHECK
				BSF		EN_PIN
				CALL	DELAY_450_US
				BCF		EN_PIN		
				BTFSC	PORTD,RD7
				GOTO	CHECK
				BCF		TRISD,TRISD7
				RETURN
				END